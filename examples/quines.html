<!DOCTYPE html>

<html>
  <head>
    
    <!-- codemirror resources -->
    <link rel="stylesheet" href="../resources/codemirror.css">
    <script src="../resources/codemirror.js"></script>
    <script src="../resources/matchbrackets.js"></script>
    <script src="../resources/closebrackets.js"></script>
    <script src="../resources/scheme.js"></script>

    <!-- veneer resources -->
    <link rel="stylesheet" href="../veneer.css"></link>
    <script type="text/javascript" src="../base.js"></script>
    <script type="text/javascript" src="../mk.js"></script>
    <script type="text/javascript" src="../eval.js"></script>
    <script type="text/javascript" src="../reader.js"></script>
    <script type="text/javascript" src="../veneer.js"></script>
    
    <!-- page specific resources -->
    <script type="text/javascript" src="quines.js"></script>
    <style type="text/css">
      p { font-size:18pt; }
      .editor { height:20em; border:1px solid #ccc; }
      .code_storage { display:none; }
    </style>

    <title>relational interpreter</title>
  </head>
  <body onload="load_page()">
    <h1>relational interpreter fun</h1>
    <p>
      With a relational interpreter you can run normal scheme functions "backwards" by grounding the output and leaving the inputs fresh. The interpreter will then produce the inputs for the given output.
    </p>
    <div id="ed1" class="editor">
      <textarea id="code1" class="code_storage">
;; running append in reverse
(eval-expo `((((lambda (f)
                 ((lambda (x)
                    (f (x x)))
                  (lambda (x)
                    (lambda (y) ((f (x x)) y)))))
               (lambda (my-append)
                 (lambda (l)
                   (lambda (s)
                     (if (null? l)
                         s
                         (cons (car l) ((my-append (cdr l)) s)))))))
              (quote ,x))
             (quote ,y))
             '()
             '(a b c d e))
 </textarea>
    </div>
    <p>
      By setting a programs input to also be it's output, you create a program which returns itself, known as a quine.
    </p>
    <div id="ed2" class="editor">
      <textarea id="code2" class="code_storage">
;; quine
(eval-expo q '() q)
</textarea>
    </div>
    <p>
      We can also create twines, which are two programs that produce the other. We make each program the output of each other and make sure they are different with a disequality constraint (otherwise we would just get two quines).
    </p>
    <div id="ed3" class="editor">
      <textarea id="code3" class="code_storage">
;; twine
(fresh ()
  (eval-expo p '() q)
  (eval-expo q '() p)
  (=/= p q))
</textarea>
    </div>
<p>Now go make some more!</p>
<p>
<img src="http://i.imgur.com/2Xdz8AT.jpg" />
</p>



<!-- hidden element holding base code -->
<textarea id="rel_interp" class="code_storage">
;; relational scheme interpreter

(define lookupo
  (lambda (x env out)
    (fresh (y val env^)
      (== `((,y . ,val) . ,env^) env)
      (symbolo x)
      (symbolo y)
      (conde
        ((== x y) (== val out))
        ((=/= x y) (lookupo x env^ out))))))


(define unboundo
  (lambda (x env)
    (fresh ()
      (symbolo x)
      (conde
        ((== '() env))
        ((fresh (y v env^)
           (== `((,y . ,v) . ,env^) env)
           (=/= x y)
           (unboundo x env^)))))))


(define eval-expo
  (lambda (expr env out)
    (fresh ()
      (conde
        ((symbolo expr) ;; variable
         (lookupo expr env out))
        ((== `(quote ,out) expr)
         (absento 'closure out)
         (unboundo 'quote env))
        ((fresh (x body) ;; abstraction
           (== `(lambda (,x) ,body) expr)
           (== `(closure ,x ,body ,env) out)
           (symbolo x)
           (unboundo 'lambda env)))
        ((fresh (expr*)
           (== `(list . ,expr*) expr)
           (unboundo 'list env)
           (eval-exp*o expr* env out)))
        ((fresh (e1 e2 val x body env^) ;; application
           (== `(,e1 ,e2) expr)
           (eval-expo e1 env `(closure ,x ,body ,env^))
           (eval-expo e2 env val)
           (eval-expo body `((,x . ,val) . ,env^) out)))
        ((fresh (e1 e2 v1 v2)
           (== `(cons ,e1 ,e2) expr)
           (== `(,v1 . ,v2) out)
           (unboundo 'cons env)
           (eval-expo e1 env v1)
           (eval-expo e2 env v2)))
        ((fresh (e v2)
           (== `(car ,e) expr)
           (unboundo 'car env)
           (eval-expo e env `(,out . ,v2))))
        ((fresh (e v1)
           (== `(cdr ,e) expr)
           (unboundo 'cdr env)
           (eval-expo e env `(,v1 . ,out))))
        ((fresh (e v)
           (== `(null? ,e) expr)
           (conde
             ((== '() v) (== #t out))
             ((=/= '() v) (== #f out)))
           (unboundo 'null? env)
           (eval-expo e env v)))
        ((fresh (t c a b)
           (== `(if ,t ,c ,a) expr)
           (unboundo 'if env)
           (eval-expo t env b)
           (conde
             ((== #f b) (eval-expo a env out))
             ((=/= #f b) (eval-expo c env out)))))))))

(define eval-exp*o
  (lambda (expr* env out)
    (conde
      ((== '() expr*) (== '() out))
      ((fresh (a d res-a res-d)
         (== (cons a d) expr*)
         (== (cons res-a res-d) out)
         (eval-expo a env res-a)
         (eval-exp*o d env res-d))))))
</textarea>
  </body>
</html>
